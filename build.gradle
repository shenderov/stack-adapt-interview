buildscript {
    ext {
        seleniumVersion = '4.1.3'
    }
}

plugins {
    id 'java'
}

group 'me.shenderov'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-opera-driver:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-edge-driver:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-ie-driver:${seleniumVersion}"
    implementation "org.seleniumhq.selenium:selenium-safari-driver:${seleniumVersion}"
    implementation "io.github.bonigarcia:webdrivermanager:5.1.1"
    implementation "org.testng:testng:7.5"
    implementation "org.apache.httpcomponents:httpclient:4.5.13"
}

//Default base URL value
def baseUrl = "https://stackadapt-interview.s3.amazonaws.com/support/Chrysler+Optical+A+D-Frame+in+Tortoiseshell+by+LINDA+FARROW+Linear+%E2%80%93+LINDA+FARROW+(U.S.).html"
//Default test suite file
def suiteFile = "all-classes.xml"

test {
    outputs.upToDateWhen { false } //always discard previous run
    systemProperty "driverType", project.hasProperty("driverType") ? project.property("driverType") : "chrome" //set driver type or use default
    systemProperty "baseUrl", project.hasProperty("baseUrl") ? project.property("baseUrl") : baseUrl //set base URL or use default
    systemProperty "hubUrl", project.hasProperty("hubUrl") ? project.property("hubUrl") : null //set Hub URL
    systemProperty "browserstackUsername", project.hasProperty("browserstackUsername") ? project.property("browserstackUsername") : null //set Browserstack username
    systemProperty "browserstackKey", project.hasProperty("browserstackKey") ? project.property("browserstackKey") : null //set Browserstack key
    systemProperty "capabilitiesFilename", project.hasProperty("capabilitiesFilename") ? project.property("capabilitiesFilename") : null //set capabilities filename
    systemProperty "jobName", project.hasProperty("jobName") ? project.property("jobName") : null //Jenkins job name to use in Test listener
    systemProperty "buildNumber", project.hasProperty("buildNumber") ? project.property("buildNumber") : null //Jenkins build number to use in Test listener
    //Config TestNG
    useTestNG() {
        useDefaultListeners = true
        suites "src/test/resources/suites/${project.hasProperty("suiteFile") && !(project.property("suiteFile") as String).isEmpty() ? project.property("suiteFile") : suiteFile}"
    }
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
    reports.html.required = true
}